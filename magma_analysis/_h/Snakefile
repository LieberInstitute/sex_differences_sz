from pathlib import Path

# Set project name

# Define paths for input files and output directory
magma_run = "/ceph/opt/magma/v1_10/magma"
Gene_Loc_File = "/ceph/projects/sex_diff_schizophrenia/analysis/magma/inputs/hg38_hg19/_m/gencode.v41lift37.genesymbol.loc"
Pop_Data_File = "/ceph/projects/sex_diff_schizophrenia/analysis/magma/inputs/_m/g1000_eur"
Gene_Input_File = "/ceph/projects/sex_diff_schizophrenia/analysis/magma/inputs/deg/_m/deg_genes.txt"

file_groups = ['ADHD','ANOREXIA','AUTISM','BIPOLAR','DEPRESSION','SCZ','HEIGHT','BMI']

rule all:
    input:
        expand("{group}_gene_set_results.gsa.out", group=file_groups)

rule create_annot_file:
    input:
        snp_loc_file = "/ceph/projects/sex_diff_schizophrenia/analysis/magma/inputs/gwas_datasets_updated/_m/{group}.snp.loc"
    output:
        annot_file = "{group}.genes.annot"
    params:
        output_prefix = "{group}"
    shell:
        """
        {magma_run} \
            --annotate window=35,10 \
            --snp-loc {input.snp_loc_file} \
            --gene-loc {Gene_Loc_File} \
            --out {params.output_prefix}
        """
        
rule create_genes_raw_file:
    input:
        annot_file = "{group}.genes.annot",
        snp_pval_file = "/ceph/projects/sex_diff_schizophrenia/analysis/magma/inputs/gwas_datasets_updated/_m/{group}.snp_pval.tsv"
    output:
        raw_file = "{group}.genes.raw"
    params:
        output_prefix = "{group}"
    shell:
        """
        {magma_run} \
            --bfile {Pop_Data_File} \
            --gene-annot {input.annot_file} \
            --gene-model snp-wise=mean \
            --pval {input.snp_pval_file} ncol=NEFF \
            --out {params.output_prefix}
        """
        
rule run_gene_set_analysis:
    input:
        genes_raw = "{group}.genes.raw"
    output:
        gene_set_results = "{group}_gene_set_results.gsa.out"
    params:
        output_prefix = "{group}_gene_set_results"
    shell:
        """
        {magma_run} \
            --gene-results {input.genes_raw} \
            --set-annot {Gene_Input_File} col=2,1 \
            --out {params.output_prefix}
        """